apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    defaultConfig {
        versionCode 2
        versionName "0.2.0"
        minSdkVersion 21

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        debug {
            versionNameSuffix '-SNAPSHOT' + new Date().format('.YMMddHHmm')
            debuggable true
        }
    }

    lintOptions {
        disable 'LongLogTag'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    compile 'com.android.support:design:23.0.1'
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.android.support:support-annotations:23.0.1'
    compile 'org.roboguice:roboguice:3.0.1'
    provided 'org.roboguice:roboblender:3.0.1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'

    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestCompile 'com.android.support.test:runner:0.4'
    androidTestCompile 'com.android.support.test:rules:0.4'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2.1'
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
}

task grantPermissions() {}

def generateGrantPermissionTask(String... permissions) {
    def adb = "${android.getSdkDirectory().getAbsolutePath()}/platform-tools/adb"
    permissions.each { permissionName ->
        def taskSuffix = permissionName
            .replaceAll(/([._])([A-Z]+)/) { all, mark, upperCase -> '.' + upperCase.toLowerCase() } // .COM_ANDROID_LIB => .com.android.lib
            .replaceAll(/(^|[.])(\w)/) { all, mark, letter -> letter.toUpperCase() }                // com.android.lib => ComAndroidLib
        tasks.grantPermissions.dependsOn task("grant$taskSuffix", type:Exec, dependsOn: 'installDebug') {
            commandLine "$adb shell pm grant pl.rbolanowski.tw4a $permissionName".split(' ')
        }
    }
}

generateGrantPermissionTask('android.permission.SET_ANIMATION_SCALE')

tasks.whenTaskAdded { task ->
    if (task.name.startsWith('connected')) {
        task.dependsOn grantPermissions
    }
}

